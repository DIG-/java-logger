plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group "br.dev.dig.logger.printer"
version versions.logger.pkg + "0"
java {
    withSourcesJar()
    compileJava {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }
}

publishing {
    publications {
        println(MavenPublication) {
            from components.java
            pom {
                name = "Simple Logger PrintLn"
                description = "Printer for Simple Logger, using System.out.println()"
                url = "https://github.com/DIG-/java-logger"
                scm {
                    url = "https://github.com/DIG-/java-logger"
                }
                licenses {
                    license {
                        name = "CC BY-ND 4.0"
                        url = "https://creativecommons.org/licenses/by-nd/4.0/"
                    }
                }
                developers {
                    developer {
                        name = "Gustavo de Oliveira Silva (DIG)"
                        email = "contato@dig.dev.br"
                    }
                }
            }
        }
    }
    repositories {
        if (deploy.central.enabled) {
            maven {
                name = "Central"
                url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                credentials {
                    username = deploy.central.username
                    password = deploy.central.password
                }
            }
        }
    }
    if (deploy.signing.enabled) {
        signing {
            useInMemoryPgpKeys((String) deploy.signing.key_id, (String) deploy.signing.secret, (String) deploy.signing.password)
            sign publishing.publications.println
        }
    }
}

dependencies {
    api lib.annotations
    api project(":logger")
}