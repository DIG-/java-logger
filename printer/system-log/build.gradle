plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}
apply from: rootDir.toString() + "/pom.gradle"

group "br.dev.dig.logger.printer"
version versions.logger.pkg + "0"
java {
    withSourcesJar()
    sourceCompatibility versions.java
    targetCompatibility versions.java
}
test {
    useJUnitPlatform()
}

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        java(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            from components.java
            pom { pom ->
                //noinspection GroovyAssignabilityCheck
                applyPom(pom, "Simple Logger System Log", "Printer for Simple Logger, using Platform Logger support")
            }
        }
    }
    repositories {
        if (deploy.central.enabled) {
            maven {
                name = "Central"
                url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                credentials {
                    username = deploy.central.username
                    password = deploy.central.password
                }
            }
        }
    }
    if (deploy.signing.enabled) {
        signing {
            useInMemoryPgpKeys((String) deploy.signing.key_id, (String) deploy.signing.secret, (String) deploy.signing.password)
            sign publishing.publications.java
        }
    }
}

task deployJavaToLocal {
    group = "deploy local"
    dependsOn ":printer:system-log:publishJavaPublicationToMavenLocal"
}

task _deployJavaToMavenCentral {
    dependsOn ":printer:system-log:publishJavaPublicationToCentralRepository"
}

task deployJavaToMavenCentral {
    group = "deploy central"
    dependsOn "checkMavenCentral"
    finalizedBy "_deployJavaToMavenCentral"
}

dependencies {
    api lib.annotations
    api project(":logger")
    api project(":printer:println")
    implementation project(":printer:stub")
    implementation project(":intrinsics")
    implementation lib.jna.core
    implementation lib.jna.platform

    testImplementation lib.test.junit
}