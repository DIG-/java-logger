plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}
apply from: rootDir.toString() + "/pom.gradle"

group "br.dev.dig.logger"
version versions.logger.pkg + "0"
java {
    withSourcesJar()
    sourceCompatibility versions.java
    targetCompatibility versions.java
}

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        java(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            from components.java
            pom { pom ->
                //noinspection GroovyAssignabilityCheck
                applyPom(pom, "Simple Logger", "Simple abstraction to log interface for java applications (support Android)")
            }
        }
    }
    repositories {
        if (deploy.central.enabled) {
            maven {
                name = "Central"
                url = "https://s01.oss.sonatype.org/content/repositories/releases/"
                credentials {
                    username = deploy.central.username
                    password = deploy.central.password
                }
            }
        }
    }
    if (deploy.signing.enabled) {
        signing {
            useInMemoryPgpKeys((String) deploy.signing.key_id, (String) deploy.signing.secret, (String) deploy.signing.password)
            sign publishing.publications.java
        }
    }
}

task deployJavaToLocal {
    group = "deploy local"
    dependsOn ":logger:publishJavaPublicationToMavenLocal"
}

task deployJavaToMavenCentral {
    group = "deploy central"
    dependsOn ":logger:publishJavaPublicationToCentralRepository"
}

dependencies {
    api lib.annotations
}