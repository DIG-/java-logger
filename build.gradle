import java.util.regex.Pattern

buildscript {
    final Properties localProp = new Properties()
    def localPropFile = (new File(rootDir, "local.properties"))
    if (localPropFile.exists()) {
        localPropFile.withInputStream { localProp.load(it) }
    }
    def findPropertyOrEnvironment = { String key ->
        findProperty(key) ?: localProp.getProperty(key) ?: System.getenv(key)
    }

    // This file must be the text version (--armor)
    String signingFilename = findPropertyOrEnvironment("GPG_SIGNING_KEY_FILE")
    File signingFile = null
    if (signingFilename != null) {
        signingFile = file(signingFilename)
        if (!signingFile.exists()) {
            signingFile = new File(rootDir, signingFilename)
            if (!signingFile.exists()) {
                signingFile = null
            }
        }
    }

    ext.isAndroidEnabled = !(new File(rootDir, "disable-android.gradle")).exists()
    ext.deploy = [
            "central": [
                    "enabled" : findPropertyOrEnvironment("MVN_CENTRAL_USERNAME") != null,
                    "username": findPropertyOrEnvironment("MVN_CENTRAL_USERNAME"),
                    "password": findPropertyOrEnvironment("MVN_CENTRAL_PASSWORD"),
            ],
            "signing": [
                    "enabled" : signingFile != null && findPropertyOrEnvironment("GPG_SIGNING_KEY_ID") != null && findPropertyOrEnvironment("GPG_SIGNING_PASSWORD") != null,
                    "secret"  : signingFile?.getText("UTF-8"),
                    "key_id"  : findPropertyOrEnvironment("GPG_SIGNING_KEY_ID"),
                    "password": findPropertyOrEnvironment("GPG_SIGNING_PASSWORD"),
            ]
    ]
    ext.versions = [
            "java"   : JavaVersion.VERSION_1_8,
            "logger" : [
                    "major": "1",
                    "minor": "0",
                    "pkg"  : "",
            ],
            "android": [
                    "minimum": 21,
                    "compile": 31,
                    "target" : 31,
            ],
            "kotlin" : "1.5.31",
            "junit"  : "5.8.2",
            "mockito": "4.4.0",
    ]
    ext.versions.logger.pkg = "${ext.versions.logger.major}.${ext.versions.logger.minor}."
    ext.lib = [
            "annotations": "org.jetbrains:annotations:20.1.0",
            "jna"        : [
                    "core"    : "net.java.dev.jna:jna:5.10.0",
                    "platform": "net.java.dev.jna:jna-platform:5.10.0",
            ],
            "test"       : [
                    "junit"  : "org.junit.jupiter:junit-jupiter:${versions.junit}",
                    "mockito": [
                            "bom"  : "org.mockito:mockito-bom:${versions.mockito}",
                            "junit": "org.mockito:mockito-junit-jupiter",
                    ],
            ]
    ]
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        if (isAndroidEnabled) {
            classpath "com.android.tools.build:gradle:7.1.3"
            classpath "de.mannodermaus.gradle.plugins:android-junit5:1.8.2.0"
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
    allprojects {
        repositories {
            mavenCentral()
            google()
        }
    }
}

Pattern regex = Pattern.compile("'([a-zA-Z0-9\\-_:]+)'")

task deploy_ToLocal {
    group = "deploy local"
    allprojects { pj ->
        for (tsk in ["deployJavaToLocal", "deployAndroidToLocal"]) {
            if (pj.getTasksByName(tsk, false).isEmpty()) {
                continue
            }
            def match = regex.matcher(pj.displayName)
            match.find()
            def name = match.group()
            dependsOn "${name.substring(1, name.length() - 1)}:$tsk"
        }
    }
    doLast {
        println("")
        println("All projects were uploaded to local")
        println("-----------------------------------")
    }
}

task generateAllJavaJars {
    group = "build"
    allprojects { pj ->
        for (tsk in ["deployJavaToLocal"]) {
            if (pj.getTasksByName(tsk, false).isEmpty()) {
                continue
            }
            def match = regex.matcher(pj.displayName)
            match.find()
            def name = match.group()
            dependsOn "${name.substring(1, name.length() - 1)}:jar"
        }
    }
}

allprojects {
    task checkMavenCentral {
        doLast {
            if (!deploy.central.enabled || !deploy.signing.enabled) {
                if (!deploy.central.enabled)
                    throw new RuntimeException("Maven central is not configured")
                throw new RuntimeException("Signing is not configured")
            }
        }
    }
}

task deploy_ToMavenCentral {
    group = "deploy central"
    dependsOn "checkMavenCentral"
    allprojects { pj ->
        for (tsk in ["deployJavaToMavenCentral", "deployAndroidToMavenCentral"]) {
            if (pj.getTasksByName(tsk, false).isEmpty()) {
                continue
            }
            def match = regex.matcher(pj.displayName)
            match.find()
            def name = match.group()
            dependsOn "${name.substring(1, name.length() - 1)}:$tsk"
        }
    }
    doLast {
        println("===========================================")
        println("All projects were uploaded to Maven Central")
        println("===========================================")
    }
}